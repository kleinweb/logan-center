# SPDX-FileCopyrightText: 2022-2023 Temple University <kleinweb@temple.edu>
# SPDX-License-Identifier: GPL-3.0-or-later

###: Nix Tasks

!include ../../.just/common.justfile

###: LINTING/FORMATTING ============================================================================

# [lint]:		Check Nix files for issues
check *FILES=prj-root: (deadnix "check" FILES) (statix "check" FILES)

# [lint]:		Write automatic linter fixes to files
lint-fix *FILES=prj-root: (deadnix "fix" FILES) (statix "fix" FILES)

# [lint]:		Run `statix` for static analysis
statix action +FILES=prj-root:
  @ # Note that stderr is silenced due to an upstream bug
  @ # https://github.com/nerdypepper/statix/issues/59
  @ for f in {{FILES}}; do \
    statix {{action}} -- "$f" 2>/dev/null; \
  done

# [lint]:		Run `deadnix` with sane options
deadnix action +FILES=prj-root:
  @deadnix \
    {{ if action == "fix" { "--edit" } else { "--fail" } }} \
    --no-lambda-pattern-names \
    {{FILES}}
