# SPDX-FileCopyrightText: 2022-2023 Temple University <kleinweb@temple.edu>
# SPDX-License-Identifier: GPL-3.0-or-later

# Load environment variables from the local `.env`
set dotenv-load

site-title := 'Logan Center for Urban Investigative Reporting'
wp-siteurl := env_var('WP_SITEURL')

admin-user := 'kleinweb'
admin-email := 'kleinweb+logancenter@temple.edu'

wp := if env_var('WP_ENV') == 'development' { 'ddev wp' } else { 'wp' }

default:
  @just --list --unsorted --color=always | rg -v "    default"

wp *ARGS:
  {{ wp }} {{ ARGS }}

wp-query-by-slug slug *ARGS:
  {{ wp }} post list --post_name={{ slug }} {{ ARGS }}

wp-install admin-password theme='logan-center-wp-next-theme':
  composer install
  {{ wp }} core install \
    --url={{ quote( wp-siteurl ) }} \
    --title={{ quote( site-title ) }} \
    --admin_user='{{ admin-user }}' \
    --admin_password='{{ admin-password }}' \
    --admin_email='{{ admin-email }}' \
    --skip-email
  {{ wp }} rewrite structure "/%year%/%monthnum%/%day%/%postname%/" && \
  {{ wp }} plugin activate --all && \
  {{ wp }} theme activate {{ theme }}

provision: _init-posts
  {{ wp }} option update show_on_front page
  {{ wp }} option update page_on_front $({{ wp }} post list --post_name=homepage --format=ids)
  wp option update page_for_posts $({{ wp }} post list --post_name=reports --format=ids)

# _init-users:

_init-posts:
  {{ wp }} post create --post_type="page" --post_title="Homepage" --post_name='homepage' \
    --post_content={{ homepage-post-content }} \
    --post_status='publish'
  {{ wp }} post create --post_type="page" --post_title="Press" --post_status="publish" --post_name=press
  {{ wp }} post create --post_type="page" --post_title="Reports" --post_status="publish" --post_name=reports

# _init-menus:
#   {{ wp }} menu create "Header"
#   {{ wp }} menu create "Footer"

homepage-post-content := quote('<!-- wp:paragraph --><p>Welcome to the homepage. Feel free to edit this page.</p><!-- /wp:paragraph -->')
